# Maintainer: Gerad Munsch <gmunsch@unforgivendevelopment.com>
#
# NOTE: based on the 'file' package from the core repo
# -- 'file' package notes --
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_pkgbase=file
pkgname=${_pkgbase}-python2
pkgver=5.17
pkgrel=1
pkgdesc="File type identification utility -- python2 bindings"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib' "file=${pkgver}")
provides=('python2-magic')
conflicts=('python2-magic' 'python2-magic-git')
source=(ftp://ftp.astron.com/pub/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('e19c47e069ced7b01ccb4db402cc01d3')

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --datadir=/usr/share/file
  make 
  cd "python"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  cd "python"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
