# Maintainer: Gerad Munsch <gmunsch@unforgivendevelopment.com>
# Old Maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vi0L0, wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert

pkgname=catalyst-dkms-test
pkgver=13.8
pkgrel=1
pkgdesc="AMD proprietary gpu kernel driver"
url="http://www.amd.com"
license=('custom')

arch=('i686' 'x86_64')

depends=('dkms')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')

conflicts=('catalyst' 'catalyst-dkms')
provides=('catalyst')

source=('http://www2.ati.com/drivers/beta/amd-catalyst-13.8-beta2-linux-x86.x86_64.zip'
        'dkms.conf'
        'flim_fglrx_13.8_proc_perm.patch'
        'lano1106_fglrx_intel_iommu.patch'
        'lano1106_kcl_agp_13_4.patch'
        'makefile_compat.patch')
sha256sums=('76e03e39804882d738b809e201732e4bc3318c7c536f9f977ef06db42d7bf978'
            '6b304e0b826ccd11942b3d4993063eaf41879f38b9eccce90e69253f6ec64816'
            'c7c58966fd8b6f847b203edd0516dbd78e6501fefb10fc53586b6d03df95f23d'
            'd7ab220204976b5802dbfa7cbd05ec0c5a9ff3524a2020b1b8deff4f601a05fa'
            '37344e6949b391a54491bcba68c2239393e442a00114ee36fe25d775d9656520'
            '9c4d95c0fd06228d261253ee6451c5e20ae9d2bca24a26133400e3312c99c7e0')
install=catalyst.install

package() {
	depends=(${depends[@]} "catalyst-utils-test=${pkgver}")
	
	cd ${srcdir}
	  
	if [ "${CARCH}" = "x86_64" ]; then
		_archdir=x86_64
	else
		_archdir=x86
	fi
	
	sh ./amd-*.run --extract fglrx-install
	
	cd fglrx-install
	
	patch -Np1 -i ../makefile_compat.patch
	patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
	patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
	patch -Np1 -i ../flim_fglrx_13.8_proc_perm.patch
	
	install -dm755 "${pkgdir}/usr/lib/modprobe.d"
	install -dm755 "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}"
	cp -r common/lib/modules/fglrx/build_mod/* "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
	cp "arch/${_archdir}"/lib/modules/fglrx/build_mod/libfglrx_ip.a "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
	cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
	sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/dkms.conf"
	  
	echo "blacklist radeon" >> "${pkgdir}/usr/lib/modprobe.d/catalyst.conf"
	  
	# license
	install -Dm644 "common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
