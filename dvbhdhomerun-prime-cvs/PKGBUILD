# Maintainer: Gerad Munsch <gmunsch@unforgivendevelopment.com>

pkgname=dvbhdhomerun-prime-cvs
true && pkgname=('dvbhdhomerun-prime-kernel-cvs' 'dvbhdhomerun-prime-user-cvs')
pkgbase=dvbhdhomerun-prime-cvs
pkgver=20130331
pkgrel=1
pkgdesc="A linux DVB driver for the HDHomeRun, with patches for the "Prime" version of the hardware (http://www.silicondust.com)"
arch=('i686' 'x86_64' 'arm')
url="http://sourceforge.net/apps/trac/dvbhdhomerun/wiki"
license=('GPL')
#provides=('dvbhdhomerun-cvs' 'dvbhdhomerun-kernel-cvs' 'dvbhdhomerun-user-cvs')
#conflicts=('dvbhdhomerun-kernel-cvs' 'dvbhdhomerun-user-cvs')
source=('hdhomerunprime.patch'
        '00-upstream-issue14.patch'
        'userhdhomerun.rc.d'
        'userhdhomerun.service')
md5sums=('68b2ea560c5bb2e5685b610642eacbc1'
         '6eb96a647422479c3e2ae6ae6743dd4a'
         '8db3071fb3c4b7603a6b1fe2b9248388'
         '84388e51257ad98505453e5782c5bf35')

makedepends=('cvs' 'libhdhomerun' 'cmake' 'linux-headers')

_cvsroot="pserver:anonymous:@dvbhdhomerun.cvs.sourceforge.net:/cvsroot"
_cvsmod=dvbhdhomerun

build() {
  cd "${srcdir}"
  cvs -z3 -d:${_cvsroot}/${_cvsmod} co -r hdhomerunprime -P ${_cvsmod}

  rm -rf ${_cvsmod}-build
  cp -a ${_cvsmod} ${_cvsmod}-build  

  cd "${srcdir}/${_cvsmod}-build"
  patch -p1 -i "${srcdir}/hdhomerunprime.patch"

  cd "${srcdir}/${_cvsmod}-build/kernel"
  make

  cd "${srcdir}/${_cvsmod}-build/userhdhomerun"
  make
}

package_dvbhdhomerun-prime-kernel-cvs() {
  provides=('dvbhdhomerun-kernel-cvs')
  conflicts=('dvbhdhomerun-kernel-cvs')
  install=dvbhdhomerun-kernel-cvs.install

  cd "${srcdir}/${_cvsmod}-build/kernel"
  make INSTALL_MOD_PATH="${pkgdir}" install

  mkdir -p "${pkgdir}/usr/lib/udev/rules.d"
  mkdir -p "${pkgdir}/etc/modules-load.d"
  install "${srcdir}/${_cvsmod}-build/debian/dvbhdhomerun-utils.udev" \
"${pkgdir}/usr/lib/udev/rules.d/dvbhdhomerun-utils.rules"
  echo "dvb_hdhomerun" > "${pkgdir}/etc/modules-load.d/dvbhdhomerun.conf"

  _device_id="$( hdhomerun_config discover | cut -d ' ' -s -f 3 )"
  if [ "${_device_id}" = "" ]
	then _device_id=FFFFFFFF
  fi
  cat > "${pkgdir}/etc/dvbhdhomerun" <<EOF
# Remember to exchange the XXXXYYYY-Z with the serial number of your HDHomeRun
# tuner:

# Auto detect tuner type is not possible in all cases, so you can force it
# DVB-C
# DVB-T
# ATSC

## EXAMPLE CONFIG ##
#[XXXXYYYY-0]
#tuner_type=ATSC
#
#[XXXXYYYY-1]
#tuner_type=ATSC
#
#[XXXXYYYY-2]
#tuner_type=ATSC

# You can use a full name including tuner ID. Meaning the tuner will appear as
# "HDHomeRun DVB-C 1234EA3D-0" instead of just "HDHomeRun DVB-C" in for example
# TVHeadend.

## EXAMPLE ##
#[XXXXYYYY-0]
#use_full_name=true

# You can disable certain tuners. As default all detected hdhomeruns are used

## EXAMPLE ##
#[XXXXYYYY-2]
#disable=true

# Default config for HDHomerun Prime, generated by `makepkg` at build time
[${_device_id}-0]
tuner_type=ATSC
use_full_name=true

[${_device_id}-1]
tuner_type=ATSC
use_full_name=true

[${_device_id}-2]
tuner_type=ATSC
use_full_name=true
EOF
}

package_dvbhdhomerun-prime-user-cvs() {
  depends=('libhdhomerun' 'gcc-libs' 'bash')
  provides=('dvbhdhomerun-user-cvs')
  conflicts=('dvbhdhomerun-user-cvs')

  cd "${srcdir}/${_cvsmod}-build/userhdhomerun"
  PREFIX=/usr make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}/etc/rc.d"
  install -m0755 "${srcdir}/userhdhomerun.rc.d" "${pkgdir}/etc/rc.d/userhdhomerun"

  mkdir -p "${pkgdir}/usr/lib/systemd/system"
  install -m0644 "${srcdir}/userhdhomerun.service" "${pkgdir}/usr/lib/systemd/system/userhdhomerun.service"
}
