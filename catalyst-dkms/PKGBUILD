# Maintainer: Gerad Munsch <gmunsch@unforgivendevelopment.com>
# Great contributor: Vi0L0 <vi0l093@gmail.com>
#                    (maintainer of main "catalyst" package(s), which this is
#                     largely based upon)
# Old maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert
# Other contributors: Any contributors mentioned in the main "catalyst" package
#                     as provided by Vi0L0 at http://goo.gl/fAKzx)

# grab kernel version
_kernver=`uname -r`

pkgname=catalyst-dkms
pkgver=14.4
pkgrel=2
_amdver=14.10.1006
pkgdesc="AMD Catalyst (proprietary GPU driver) kernel driver (DKMS version) [NOTE: Radeon HD 5xxx+ ONLY]"
arch=('i686' 'x86_64')
url="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
license=('custom')
options=('staticlibs' 'libtool' '!upx' '!strip')
depends=('dkms'
         'linux>=3.0'
         'linux<3.15')
makedepends=('gcc-libs'
             'gcc>4.0.0')
optdepends=('linux-headers: build the module against Arch kernel (requires at least one set of kernel headers)'
            'linux-ck-headers: build the module against Linux-ck kernel (requires at least one set of kernel headers)'
            'linux-lts-headers: build the module against LTS Arch kernel (requires at least one set of kernel headers)')

# try to ensure that this package cannot be installed concurrently with any of
# the other 'catalyst'-series packages
conflicts=('catalyst'
           'catalyst-dkms-test'
           'catalyst-test'
           'catalyst-test-pxp'
           'catalyst-total'
           'catalyst-total-pxp')
provides=('catalyst')

source=("http://www2.ati.com/drivers/linux/amd-catalyst-${pkgver/./-}-linux-x86-x86-64.zip"
        'dkms.conf'
        'lano1106_fglrx_intel_iommu.patch'
        'lano1106_kcl_agp_13_4.patch'
        'lano1106_fglrx-13.8_proc.patch'
        'cold-fglrx-3.14-current_euid.patch')

sha384sums=('532e912d8550a526d1b96b4962d28ca91866de2309816c97a50564000a0156ea72943ffb2c77e74ccf6697d44a5f7ca9'
            'f1ff04347eb04b56a4e0bd6a33200c5d4d72135557867634c20447d07a8aeb1da417340e7d086809fdda644ae7cd4da7'
            'f3b754f5d2edb5f0950ce49dcdcf19b1d7db6618c267623d7c4b7c365f14de8726dbd780e00599cd656d431af4f7fc78'
            '0dbd2f121237e4246434770e5f53a249a2e1a05ee5dd43e3757e0d3192eb0f89a9c3ed097f67962f5e0a6bb96554710c'
            'c3c726dd3aae09b43f36b7226b0fab4a73e32433acd815acbda9bbe070728ff487bcd6b0383ff601286cb2298983aa7a'
            '28239ec762ba5d832bbae3365b1d5479256786fa2a42fd94921708630147abdd3adc2f82a3cb198aba9b9573076d95ca')

install=catalyst.install

# AMD changed the way we need to download our package, we now have to pass a referer.
url_ref="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="http::/usr/bin/curl --referer ${url_ref} -o %o %u"

package() {
  depends=(${depends[@]} "catalyst-utils=${pkgver}")

  cd ${srcdir}

  # determine architecture
  if [ "${CARCH}" = "x86_64" ]; then
    _archdir=x86_64
  else
    _archdir=x86
  fi

  # extract files from installer
  /bin/sh ./fglrx-${_amdver}/amd-driver-installer-${_amdver}-x86.x86_64.run --extract fglrx_installer-files

  cd fglrx_installer-files

  # patch sources
  patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
  patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
  patch -Np1 -i ../lano1106_fglrx-13.8_proc.patch
  patch -Np1 -i ../cold-fglrx-3.14-current_euid.patch

  # install some directories
  install -dm755 "${pkgdir}/usr/lib/modprobe.d"
  install -dm755 "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}"

  # copy sources
  cp -r common/lib/modules/fglrx/build_mod/* "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp "arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"

  # copy dkms.conf and set version
  cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/dkms.conf"

  # blacklist open-source radeon module from loading
  echo "blacklist radeon" >> "${pkgdir}/usr/lib/modprobe.d/catalyst.conf"

  # install license
  install -Dm644 "common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
