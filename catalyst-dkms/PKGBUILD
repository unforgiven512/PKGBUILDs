# Maintainer: Gerad Munsch <gmunsch@unforgivendevelopment.com>
# Great contributor: Vi0L0 <vi0l093@gmail.com>
#                    (maintainer of main "catalyst" package(s), which this is
#                     largely based upon)
# Old maintainer: Jameson Pugh <imntreal@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert
# Other contributors: Any contributors mentioned in the main "catalyst" package
#                     as provided by Vi0L0 at http://goo.gl/fAKzx)

# grab kernel version
_kernver=`uname -r`

pkgname=catalyst-dkms
pkgver=13.12
pkgrel=1
pkgdesc="AMD proprietary gpu kernel driver (DKMS version)"
arch=('i686' 'x86_64')
url="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
license=('custom')
depends=('dkms')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
replaces=('catalyst-lts-dkms') # useless
conflicts=('catalyst-dkms-test'
           'catalyst-test'
           'catalyst')

source=("http://www2.ati.com/drivers/linux/amd-catalyst-${pkgver}-linux-x86.x86_64.zip"
        "dkms.conf"
        "lano1106_fglrx_intel_iommu.patch"
        "lano1106_kcl_agp_13_4.patch"
        "lano1106_fglrx-13.8_proc.patch"
        "arch_3.13_kernel_acpi_node.patch"
        "looks_like_amd_forgot_this.patch")

sha256sums=('024033f4847d1c2a182fc44e0b3df29b2d133e24aeaba390f4504a8f3361a0ca'
            '6b304e0b826ccd11942b3d4993063eaf41879f38b9eccce90e69253f6ec64816'
            'd7ab220204976b5802dbfa7cbd05ec0c5a9ff3524a2020b1b8deff4f601a05fa'
            '37344e6949b391a54491bcba68c2239393e442a00114ee36fe25d775d9656520'
            '67504f6dbf9f880d0dda7e85e372abb86088fc033ba2c30d28c425c36950985b'
            '61d223007c03b1bbdaf22509aeeb60113b0f528d6d5153fe1d7471223a1b0126'
            'd8811b23d646966899ee348c2b277333d347e902f321ded14e38f3cd00e15762')

install=catalyst.install

# AMD changed the way we need to download our package, we now have to pass a referer.
url_ref="http://support.amd.com/en-us/download/desktop?os=Linux+x86"
DLAGENTS="http::/usr/bin/curl --referer ${url_ref} -o %o %u"

package() {
  depends=(${depends[@]} "catalyst-utils=${pkgver}")

  cd ${srcdir}


  # determine architecture
  if [ "${CARCH}" = "x86_64" ]; then
    _archdir=x86_64
  else
    _archdir=x86
  fi


  # extract files from installer
  /bin/sh ./amd-catalyst-${pkgver}-linux-x86.x86_64.run --extract fglrx_installer-files

  cd fglrx_installer-files

  # install some directories
  install -dm755 "${pkgdir}/usr/lib/modprobe.d"
  install -dm755 "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}"

  # patch sources
  patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
  patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
  patch -Np1 -i ../lano1106_fglrx-13.8_proc.patch
  patch -Np1 -i ../arch_3.13_kernel_acpi_node.patch
  patch -Np1 -i ../looks_like_amd_forgot_this.patch

  # copy sources
  cp -r common/lib/modules/fglrx/build_mod/* "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp "arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"

  # copy dkms.conf and set version
  cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/dkms.conf"

  # blacklist open-source radeon module from loading
  echo "blacklist radeon" >> "${pkgdir}/usr/lib/modprobe.d/catalyst.conf"
  
  # install license
  install -Dm644 "common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
